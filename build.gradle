plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'xyz.jpenilla.run-paper' version '1.0.6'
}

group = 'net.geferon.minecraft'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"

    implementation("com.google.inject:guice:5.1.0") {
        exclude(group: "com.google.guava", module: "guava")
    }
    implementation("com.google.inject.extensions:guice-assistedinject:5.1.0")
    implementation("org.mariadb.jdbc:mariadb-java-client:3.1.3")
    implementation("com.zaxxer:HikariCP:5.0.1")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
tasks.runServer.minecraftVersion("1.19.4")

shadowJar {
    classifier = 'dist'
    dependencies {
        include(dependency("aopalliance:aopalliance"))
        include(dependency("com.google.inject:guice"))
        include(dependency("com.google.inject.extensions:guice-assistedinject"))
        include(dependency("org.mariadb.jdbc:mariadb-java-client"))
        include(dependency("com.zaxxer:HikariCP"))
    }
    relocate("com.google.inject", "net.geferon.libs.guice")
    relocate("org.aopalliance.intercept", "net.geferon.libs.intercept")
    relocate("org.mariadb", "net.geferon.libs.mariadb")
    relocate("com.zaxxer.hikari", "net.geferon.libs.hikari")
}
build.dependsOn(shadowJar)
